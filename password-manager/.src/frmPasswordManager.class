' Gambas class file

Public Sub Form_Open()

  ModDB.Connect()
  dtSource.Connection = ModDB.$Con
  dtSource.Table = "saved_password"
  dtSource.Filter = "login_id = " & ModDB.$CurrentUserID
  dtView.Columns = ["url"]
  dtView.Labels = ["Website URL"]
  dtView.View.Columns[0].Expand = True
  
End

Public Sub btnSavePassword_Click()

  Dim res As Result
  
  ''' Check if the fields are empty or not
  If txtURL.Text = "" Or txtName.Text = "" Or txtPassword.Text = "" Then 
    Message.Warning("All fields are required!")
    Return 
  Endif
  
  ''' To create a new data provided to the fields
  res = ModDB.$Con.Create("saved_password")
  res["url"] = txtURL.Text
  res["name"] = txtName.Text
  res["password"] = txtPassword.Text
  res["login_id"] = ModDB.$CurrentUserID
  
  ''' Update the database
  Try res.Update()
  
  ''' To catch database error
  If Error Then 
    Message.Error("Failed to save the password: " & Error.Text)
  Else 
    Message.Info("Password saved for " & txtURL.Text, "OK")
    txtURL.Clear()
    txtName.Clear()
    txtPassword.Clear()
    tabStripPasswordManager.Index = 1
    dtSource.Update()
  Endif 
  
End


Public Sub chbShowPassword_Click()

  If chbShowPassword.Value = True Then 
    txtPassword.Password = False
  Else 
    txtPassword.Password = True
  Endif

End

Public Sub dtView_MouseDown()

  Dim sSelectedWebsite As String
  Dim res As Result
  
  sSelectedWebsite = dtView.View[dtView.View.Row, 0].Text
  res = ModDB.$Con.Find("saved_password", "url = &1 AND login_id = &2", sSelectedWebsite, ModDB.$CurrentUserID)
  
  If res.Available Then 
    txtSavedID.Text = res!id
    txtSavedURL.Text = res!url
    txtSavedUserName.Text = res!name
    txtSavedPassword.Text = res!password
  Endif
  
End

Public Sub btnUpdate_Click()

  Dim res As Result
  
  res = ModDB.$Con.Edit("saved_password", "id = &1 AND login_id = &2", txtSavedID.Text, ModDB.$CurrentUserID)
  
  If res.Available Then 
    res!url = txtSavedURL.Text
    res!name = txtSavedUserName.Text
    res!password = txtSavedPassword.Text
    Message.Info("Record updated!", "OK")
    res.Update()
    dtSource.Filter = "login_id = " & ModDB.$CurrentUserID
    
  Else 
    Message.Error("Could not find the record to update.")
  Endif
  
  Catch 
    Message.Error("Error updating the database: " & Error.Text)
  
End

Public Sub btnDelete_Click()

  If Message.Question("Are you sure you want to delete this record?", "Yes", "No") = 1 Then 
    ModDB.$Con.Delete("saved_password", "id = &1", txtSavedID.Text)
    Message.Info("Record deleted!", "OK")
    txtSavedID.Clear()
    txtSavedURL.Clear()
    txtSavedUserName.Clear()
    txtSavedPassword.Clear()
    dtView.SetFocus()
    dtSource.Filter = "login_id = " & ModDB.$CurrentUserID
  Endif
  
  Catch
    Message.Error("Error updating the database: " & Error.Text)
  
End

Public Sub btnUpdatePassword_Click()

  Me.Hide()
  frmUpdateAccountPassword.Show()
  
End

Public Sub btnLogout_Click()

  Me.Close() ' it will also call the Form_Close() event where the condition to logout or not is found
  
End

Public Sub Form_Close()

  If Message.Question("Are your sure you want to logout?", "Logout", "Cancel") = 1 Then 
    ModDB.$CurrentUserID = 0
    Me.Close()
    frmLogin.Show()
  Else 
    Stop Event ' this stops the Form_Close() event from closing the window  
  Endif

End

Public Sub txtSearch_Change() ' this is the code for searching in the dataview

  If Not txtSearch.Text Then
    dtSource.Filter = "login_id = " & ModDB.$CurrentUserID
    Return 
  Endif
  
  ' Filter using the LIKE operator for partial matches
  ' We use LOWER so the search isn't case-sensitive: sqlite uses LOWER and GAMBAS uses LCASE to check for lowercase letters
  ' The % symbols are wildcards (meaning "anything before or after this text")
  dtSource.Filter = Subst$("login_id = &1 AND LOWER(url) LIKE LOWER('%&2%')", ModDB.$CurrentUserID, txtSearch.Text)

End
